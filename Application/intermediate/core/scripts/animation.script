global create_animation = function(id)
{
    local animation = 
    {
        self = id,
    };

    animation.start = function()
    {
        print("animation start.");
        .shading_env = resource.find_resource(string_to_id("shading_enviroment"), string_to_id("core/common/default"));
        world.set_shading_enviroment(.shading_env);
        physics.create_world(string_to_id("core/global"));
        physics.create_plane(500);
        graphics.camera_set_position(0, 10, -10);
        graphics.camera_set_lookat(0, 0, 0);
        this:stateSet(.running_state);
    };

    animation.stop = function()
    {
        print("animation stop.");
    };

    animation.running_state = function()
    {
        print("enter ---------------> running state ");

        local w = get_window_width();
        local h = get_window_height();  
        local debug_index = 0;
        
        local bShowProfile = false;
        while(true)
        {
            if(input.is_key_just_pressed(input.F1))
            {
                graphics.set_debug_mode(debug_index);
                debug_index += 1;
                if(debug_index > 4)
                {
                    debug_index = 0;
                }
            }
            else if(input.is_key_just_pressed(input.ESC))
            {
                shut_down();
            }
            else if(input.is_key_just_pressed(input.F2))
            {
                bShowProfile = !bShowProfile;
            }

            graphics.update_debug_camera();
            graphics.debug_add_axis(0,0,0,0,0,0,10);
            graphics.debug_draw_models();
            graphics.debug_draw_lights();
            if(bShowProfile)
            {
                show_profile();
            }

            yield();
        }  
    };
    return animation;
};